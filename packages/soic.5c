/*
 * Copyright Â© 2012 Keith Packard <keithp@keithp.com>
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; version 2 of the License.
 *
 * This program is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along
 * with this program; if not, write to the Free Software Foundation, Inc.,
 * 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA.
 */

load "footprint.5c"
import Footprint;

int total_pad = 16;
real center_gap = 4.00;

if (dim(argv) > 1)
	total_pad = string_to_integer(argv[1]);
if (dim(argv) > 2)
	center_gap = string_to_real(argv[2]);

real pad_width = 0.60;

real pad_height;

if (center_gap > 6)
	pad_height = 1.50;
else
	pad_height = 1.30;

real pad_spacing = 1.27;

int num_pad = total_pad / 2;	/* per side */

string name = sprintf ("SOIC-%d", total_pad);

real package_width = (num_pad - 1) * pad_spacing + 1.61;

real package_height = center_gap + 2 * pad_height + 0.4;

real pad_y_off = center_gap / 2 + pad_height / 2;

element_start(name);

real pad_off(int n) {
	return pad_spacing * (n - (num_pad - 1) / 2);
}

real corner(int dx, int dy) {
	real	x = dx * package_width / 2;
	real	y = dy * package_height / 2;
	real	len = 0.4;

	line(x, y, x - dx * len, y);
	line(x, y, x, y - dy * len);
}

corner(-1, -1);
corner(1, -1);
corner(-1, 1);
corner(1, 1);

real dot_off = -.3;

line(-package_width/2 + dot_off, package_height/2 - dot_off,
     -package_width/2 + dot_off, package_height/2 - dot_off);

for (pad = 0; pad < num_pad; pad++) {
	/* bottom (1-num_pad) */

	pad_mm(pad_off(pad),
	       pad_y_off,
	       pad_width,
	       pad_height,
	       sprintf("%d", pad + 1),
	       sprintf("%d", pad + 1));
	
	/* top side (total_pad - num_pad+1) */

	pad_mm(-pad_off(pad),
	       -pad_y_off,
	       pad_width,
	       pad_height,
	       sprintf("%d", total_pad - pad),
	       sprintf("%d", total_pad - pad));
	
}

element_end();
